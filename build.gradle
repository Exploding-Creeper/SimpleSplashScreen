plugins {
    id 'java'
    id "xyz.wagyourtail.unimined" version "1.1.0-SNAPSHOT" apply false
    id "com.hypherionmc.modutils.modpublisher" version "2.0.+" apply false
}

ext {
    release = project.properties['release'] ?: false
}

var base_version = "${version_major}.${version_minor}"
var final_version = "${base_version}.${version_patch}"

// CI Support
if (!release && System.getenv('BUILD_NUMBER') != null) {
    final_version = "${base_version}.${System.getenv('BUILD_NUMBER')}d"
}

group = project_group
version = "${final_version}"

subprojects {
    apply plugin: "xyz.wagyourtail.unimined"
    apply plugin: "java"
    apply plugin: 'maven-publish'
    apply plugin: 'com.hypherionmc.modutils.modpublisher'

    group = rootProject.group
    version = rootProject.version

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    // Add your maven repositories here
    repositories {
        mavenCentral()

        maven {
            url "https://mcentral.firstdark.dev/releases"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'
    }

    jar {
        manifest {
            attributes([
                    'Specification-Title'     : project.archivesBaseName,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : project.version,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : project.version,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestamp'               : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    /**
     * ===============================================================================
     * =       DO NOT EDIT BELOW THIS LINE UNLESS YOU KNOW WHAT YOU ARE DOING        =
     * ===============================================================================
     */

    unimined.minecraft(sourceSets.main, true) {
        version minecraft_version

        mappings {
            mojmap()
            devNamespace "mojmap"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    clean {
        delete "$rootDir/artifacts"
    }

    if (project.name !== 'Common') {
        tasks.register('delDevJar') {
            doLast {
                def tree = fileTree('build/libs')
                tree.include '**/*-dev-shadow.jar'
                tree.include '**/*-dev.jar'
                tree.include '**/*-all.jar'
                tree.include '**/*-slim.jar'
                tree.each { it.delete() }
            }
        }
        build.finalizedBy delDevJar

        tasks.register('copyAllArtifacts', Copy) {
            from "$buildDir/libs"
            into "$rootDir/artifacts"
            include("*.jar")
        }

        build.finalizedBy(copyAllArtifacts)
    }
}
