buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

plugins {
    id "me.hypherionmc.modutils.modpublisher" version "1.0.7"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'signing'

group = 'me.hypherionmc'
archivesBaseName = "SimpleSplashScreen-${project.minecraft_version}"

ext {
    release=project.properties['release'] ?: false
}

version = modversion + (modversion_patch > 0 ? ".${modversion_patch}" : "")

// Jenkins
if (!release && System.getenv('BUILD_NUMBER') != null) {
    version = modversion + "." + System.getenv('BUILD_NUMBER') + "d"
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '17'

minecraft {
    mappings channel: 'official', version: minecraft_version
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=simplesplashscreen.mixins.json"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                simplesplashscreen {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                simplesplashscreen {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'simplesplashscreen', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                simplesplashscreen {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven {
        name = 'Sponge / Mixin'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation(fg.deobf("me.shedaniel.cloth:cloth-config-forge:${cloth_version}"))
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "simplesplashscreen",
                "Specification-Vendor"    : "HypherionSA",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "HypherionSA",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "simplesplashscreen.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

mixin {
    add sourceSets.main, "simplesplashscreen.refmap.json"
}

if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}

publisher {
    apiKeys {
        curseforge = System.getenv("CURSE_TOKEN")
        modrinth = System.getenv("MODRINTH_TOKEN")
    }

    curseID = "503390"
    modrinthID = "4uhcFYch"
    versionType = "release"
    changelog = rootProject.file("changelog.md")
    version = "${minecraft_version}-${project.version}"
    displayName = "[1.19.3] Simple Splash Screen ${project.version}"
    gameVersions = ["1.19.3"]
    loaders = ["forge"]
    artifact = jar

    modrinthDepends {
        required = ["9s6osm5g"]
    }

    curseDepends {
        optional = ["cloth-config"]
    }
}

publishMod.dependsOn(build)
